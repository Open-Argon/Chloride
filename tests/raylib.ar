# SPDX-FileCopyrightText: 2025 William Bell
#
# SPDX-License-Identifier: GPL-3.0-or-later

import "../stdlib/raylib" expose raylib
import "../stdlib/random" expose random
import "../stdlib/maths" as maths

term.log(raylib)

class bouncing_text do
  this.__init__(self, text, pos, speed) = do
    self.text = text
    self.pos = pos
    self.speed = speed
    self.color=raylib.Color(random.int(0,255),random.int(0,255),random.int(0,255),255)
  this.render(self, frame, max) = raylib.DrawText(
    self.text, maths.round(max[0] - maths.abs(((self.pos[0]+frame * self.speed) % (2 * max[0])) - max[0])),
    maths.round(max[1] - maths.abs(((self.pos[1]+frame * self.speed) % (2 * max[1])) - max[1])), 20,
    self.color
  )


class Game do
  this.__init__(self, width, height, fps) = do
    self.frame = 0
    self.width = width
    self.height = height
    raylib.InitWindow(self.width, self.height, "Hello Raylib")
    if (fps) raylib.SetTargetFPS(fps)
    self.bouncing_texts = []
    let x = 0
    while (x<30) do
      let y = 0
      while (y<30) do
        self.bouncing_texts.append(bouncing_text(string(x*y), tuple(x*10,y*10), (x+y+1)))
        y=y+1
      x=x+1

  this.render(self) = do
      # term.log("frame:",self.frame)
      let maxX = self.width -10, maxY = self.height-20
      raylib.ClearBackground(raylib.BLACK)

      let i = 0
      while (i<self.bouncing_texts.length) do
        self.bouncing_texts[i].render(self.frame,tuple(maxX, maxY))
        i=i+1

      self.frame=self.frame+1
  
  this.__string__(self) = do
    return "game instance"


let instance = Game(820, 640, 0)

while (!raylib.WindowShouldClose()) do
  raylib.BeginDrawing()
  instance.render()
  raylib.EndDrawing()

raylib.CloseWindow()

term.log("bye :)")